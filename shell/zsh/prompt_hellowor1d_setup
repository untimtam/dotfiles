#
# Tweak of Paradox theme.
#
# Authors:
#   Tim Flichy <timflichy@gmail.com>
#

# Load dependencies.
pmodload 'helper'

# Define variables.
_prompt_helloworld_current_bg='NONE'
_prompt_helloworld_segment_separator=''

function prompt_helloworld_start_segment {
  local bg fg
  [[ -n "$1" ]] && bg="%K{$1}" || bg="%k"
  [[ -n "$2" ]] && fg="%F{$2}" || fg="%f"
  if [[ "$_prompt_helloworld_current_bg" != 'NONE' && "$1" != "$_prompt_helloworld_current_bg" ]]; then
    print -n " $bg%F{$_prompt_helloworld_current_bg}$_prompt_helloworld_segment_separator$fg "
  else
    print -n "$bg$fg "
  fi
  _prompt_helloworld_current_bg="$1"
  [[ -n "$3" ]] && print -n "$3"
}

function prompt_helloworld_end_segment {
  if [[ -n "$_prompt_helloworld_current_bg" ]]; then
    print -n " %k%F{$_prompt_helloworld_current_bg}$_prompt_helloworld_segment_separator"
  else
    print -n "%k"
  fi
  print -n "%f"
  _prompt_helloworld_current_bg=''
}

function prompt_helloworld_build_prompt {
  prompt_helloworld_start_segment black default '%(?::%F{red}✘ )%(!:%F{yellow}⚡ :)%(1j:%F{cyan}⚙ :)%F{blue}%n%F{13}@%F{green}%m%f'
  prompt_helloworld_start_segment blue black '$_prompt_helloworld_pwd'

  if [[ -n "$git_info" ]]; then
    prompt_helloworld_start_segment green black '${(e)git_info[ref]}${(e)git_info[status]}'
  fi

  prompt_helloworld_end_segment
}

function prompt_helloworld_pwd {
  local pwd="${PWD/#$HOME/~}"

  if [[ "$pwd" == (#m)[/~] ]]; then
    _prompt_helloworld_pwd="$MATCH"
    unset MATCH
  else
    _prompt_helloworld_pwd="${${${${(@j:/:M)${(@s:/:)pwd}##.#?}:h}%/}//\%/%%}/${${pwd:t}//\%/%%}"
  fi
}

function prompt_helloworld_precmd {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS

  # Format PWD.
  prompt_helloworld_pwd

  # Get Git repository information.
  if (( $+functions[git-info] )); then
    git-info
  fi

  # Get Node info
  if (( $+functions[node-info] )); then
    node-info
  fi

  # Get Ruby info
  if (( $+functions[ruby-info] )); then
    ruby-info
  fi

  # Get Python info
  if (( $+functions[python-info] )); then
    python-info
  fi
}

function prompt_helloworld_setup {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS
  prompt_opts=(cr percent subst)

  # Load required functions.
  autoload -Uz add-zsh-hook

  # Add hook for calling git-info before each command.
  add-zsh-hook precmd prompt_helloworld_precmd

  # Set editor-info parameters.
  zstyle ':prezto:module:editor:info:completing' format '%B%F{red}...%f%b'
  zstyle ':prezto:module:editor:info:keymap:primary' format '%B%F{blue}❯%f%b'
  zstyle ':prezto:module:editor:info:keymap:primary:overwrite' format '%F{red}♺%f'
  zstyle ':prezto:module:editor:info:keymap:alternate' format '%B%F{red}❮%f%b'

  # Set git-info parameters.
  zstyle ':prezto:module:git:info' verbose 'yes'
  zstyle ':prezto:module:git:info:action' format ' ⁝ %s'
  zstyle ':prezto:module:git:info:added' format ' ✚'
  zstyle ':prezto:module:git:info:ahead' format ' ⬆'
  zstyle ':prezto:module:git:info:behind' format ' ⬇'
  zstyle ':prezto:module:git:info:branch' format ' %b'
  zstyle ':prezto:module:git:info:commit' format '➦ %.7c'
  zstyle ':prezto:module:git:info:deleted' format ' ✖'
  zstyle ':prezto:module:git:info:dirty' format ' ⁝'
  zstyle ':prezto:module:git:info:modified' format ' ✱'
  zstyle ':prezto:module:git:info:position' format '%p'
  zstyle ':prezto:module:git:info:renamed' format ' ➙'
  zstyle ':prezto:module:git:info:stashed' format ' S'
  zstyle ':prezto:module:git:info:unmerged' format ' ═'
  zstyle ':prezto:module:git:info:untracked' format ' ?'
  zstyle ':prezto:module:git:info:keys' format \
    'ref' '$(coalesce "%b" "%p" "%c")' \
    'status' '%s%D%A%B%S%a%d%m%r%U%u'

  # Set node-info parameters
  zstyle ':prezto:module:node:info:version' format '%F{106}node:%v%f'

  # Set ruby-info parameters.
  zstyle ':prezto:module:ruby:info:version' format '%F{red}rb:%v%f'

  # Set python-info parameters.
  zstyle ':prezto:module:python:info:virtualenv' format '%F{220}py:%v%f'

  PROMPT='${(e)$(prompt_helloworld_build_prompt)}
 ${editor_info[keymap]} '
  RPROMPT='%F{blue}[%F{cyan}%D{%H:%M:%S}%F{blue}]%f'
  SPROMPT='zsh: correct %F{red}%R%f to %F{green}%r%f [nyae]? '
}

prompt_helloworld_setup "$@"
